apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.bintray'
apply plugin: "maven"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion "25.0.3"//9: added
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    //testOptions {
        //unitTests {
            //includeAndroidResources = true
        //}
    //}9: removed testing
}

group = 'com.github.bassaer'
version = rootProject.ext.versionName

def siteUrl = 'https://github.com/bassaer/ChatMessageView'
def gitUrl = 'https://github.com/bassaer/ChatMessageView.git'
def issueUrl = 'https://github.com/bassaer/ChatMessageView/issues'

bintray {
    user = 'bassaer'
    key = System.getenv('BINTRAY_API_KEY') // Travis ENV
    publish = true
    configurations = ['archives']
    pkg {
        repo = 'ChatMessageView'
        name = project.name
        userOrg = 'tnakayama'
        desc = 'Chat UI Library for Android'
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['android']
        publicDownloadNumbers = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "androidx.core:core:$androidXCoreVersion"
    compile "androidx.appcompat:appcompat:$androidXAppcompatVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "de.hdodenhof:circleimageview:$circleimageviewVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    testCompile "junit:junit:$junitVersion"
    //testCompile "org.robolectric:robolectric:$robolectricVersion"
    //testCompile "androidx.test:core:$androidXTestCoreVersion"
    //testCompile "org.mockito:mockito-core:$mockitoCoreVersion"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs.findAll {
        //exclude kotlin files
        !(it.name.contains('kotlin'))
    }
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions doLast {
    println project.getConvention()
}

def repo = new File(rootDir, "repository")

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file://${repo.absolutePath}"
            pom.version = rootProject.ext.versionName
            pom.groupId = 'com.github.bassaer'
            pom.artifactId = 'chatmessageview'
        }
    }
}

repositories {
    mavenCentral()
    //11:changed
    maven {
        url 'https://maven.google.com'
    }
}
